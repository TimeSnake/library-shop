plugins {
  id 'java'
  id 'java-base'
  id 'java-library'
  id 'maven-publish'
  id("io.papermc.paperweight.userdev") version "1.5.5"
  id("xyz.jpenilla.run-paper") version "2.1.0"
  id('com.github.johnrengelman.shadow') version "8.1.1"
}


group = "de.timesnake"
version = '0.1'
archivesBaseName = 'library-shop'
var projectId = 41

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url 'https://papermc.io/repo/repository/maven-public/'
  }
  maven {
    url "https://git.timesnake.de/api/v4/groups/7/-/packages/maven"
    name "TimeSnake"
    credentials {
      username = timesnakeUser
      password = timesnakePassword
    }
  }
}

dependencies {
  compileOnly "de.timesnake:basic-loungebridge:1.3.1"
  compileOnly "de.timesnake:basic-game:1.2.1"
  compileOnly "de.timesnake:basic-bukkit:3.1.2"

  compileOnly "de.timesnake:database-bukkit:2.2.2"
  compileOnly "de.timesnake:database-api:2.2.2"

  compileOnly "de.timesnake:channel-bukkit:3.0.6"
  compileOnly "de.timesnake:channel-api:3.0.6"

  compileOnly "de.timesnake:library-game:1.2"
  compileOnly "de.timesnake:library-packets:2.1.1"
  compileOnly "de.timesnake:library-entities:2.2"
  compileOnly "de.timesnake:library-extension:1.2"
  compileOnly "de.timesnake:library-basic:1.2.2"
  compileOnly "de.timesnake:library-chat:1.1"

  paperweight.paperDevBundle("1.20.1-R0.1-SNAPSHOT")
}

configurations.configureEach {
  resolutionStrategy.dependencySubstitution {
    if (project.parent != null) {
      substitute module("de.timesnake:basic-bukkit") using project(":basic-bukkit")
      substitute module("de.timesnake:database-bukkit") using project(":database-bukkit")
      substitute module("de.timesnake:database-api") using project(":database-api")
      substitute module("de.timesnake:channel-bukkit") using project(":channel-bukkit")
      substitute module("de.timesnake:channel-api") using project(":channel-api")
      substitute module("de.timesnake:library-game") using project(":library-game")
      substitute module("de.timesnake:library-packets") using project(":library-packets")
      substitute module("de.timesnake:library-entities") using project(":library-entities")
      substitute module("de.timesnake:library-extension") using project(":library-extension")
      substitute module("de.timesnake:library-basic") using project(":library-basic")
      substitute module("de.timesnake:library-chat") using project(":library-chat")
    }
  }
}

tasks {
  jar {
    dependsOn(reobfJar)
  }

  compileJava {
    options.encoding = "UTF-8"
    options.release.set(17)
  }

  javadoc {
    options.encoding = "UTF-8"
  }

  processResources {
    filteringCharset = "UTF-8"
    def props = [
        "name"      : archivesBaseName,
        "version"   : project.version,
        "apiVersion": "1.20.1"
    ]
    inputs.properties(props)
    filesMatching("plugin.yml") {
      expand(props)
    }
  }
}

tasks.register('exportAsPlugin', Copy) {
  from "$buildDir/libs/${archivesBaseName}-${project.version}.jar"
  into "${timesnakePluginDir}"

  dependsOn(assemble)
}

publishing {
  repositories {
    maven {
      url "https://git.timesnake.de/api/v4/projects/" + projectId + "/packages/maven"
      credentials(HttpHeaderCredentials) {
        name = "Job-Token"
        value = System.getenv("CI_JOB_TOKEN")
      }
      authentication {
        header(HttpHeaderAuthentication)
      }
    }
  }

  publications {
    mavenJava(MavenPublication) {
      from components.java

      artifact "$buildDir/libs/${archivesBaseName}-${project.version}.jar"
    }
  }
}

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'
}

java {
  withSourcesJar()
}